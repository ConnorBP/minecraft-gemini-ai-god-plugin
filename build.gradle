plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'java'
}

sourceCompatibility = JavaLanguageVersion.of(java_version as int)
targetCompatibility = JavaLanguageVersion.of(java_version as int)

archivesBaseName = archives_base_name
version = plugin_version
group = maven_group

processResources {
    filesMatching("plugin.yml") {
        expand "version": plugin_version,
                "bukkit_api_version": bukkit_api_version
    }
}

// fix carls stupid custom domain name
configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'me.carleslc.Simple-YAML' && details.requested.name == 'Simple-Yaml') {
            details.useTarget 'com.github.Carleslc.Simple-YAML:Simple-Configuration:1.8.1'
        }
    }
}

dependencies {
    implementation 'com.github.mizosoft.methanol:methanol:1.7.0'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'
    implementation 'org.apache.httpcomponents:httpasyncclient:4.1.5'
    implementation 'commons-io:commons-io:2.15.1'
    // To use this dependency, you need to compile bukkit by yourself
    // See https://www.spigotmc.org/wiki/buildtools/
    // implementation "org.bukkit:craftbukkit:${bukkit_version}"

    // Use this dependency if you don't want to compile bukkit
    implementation "io.papermc.paper:paper-api:${bukkit_version}"
    implementation "de.maxhenkel.voicechat:voicechat-api:${voicechat_api_version}"
    implementation "com.github.jensjeflensje:minecraft_imagery:${minecraft_imagery_api_version}"
    implementation "com.loohp:ImageFrame:${image_frame_api_version}"
	// implementation 'me.carleslc.Simple-YAML:Simple-Yaml:1.8.1'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation group: 'com.knuddels', name: 'jtokkit', version: '1.0.0'

}

repositories {
    mavenCentral()
    // for voice chat api
    maven {
        name = "henkelmax.public"
        url = 'https://maven.maxhenkel.de/repository/public'
    }
    // for ImageFrame api
    maven {
        name = "loohp-repo"
        url = "https://repo.loohpjames.com/repository"
    }
    // You need this maven repository if you want to use the paper dependency
    maven {
        url = uri("https://papermc.io/repo/repository/maven-public/")
    }
    // for github and stuff
    maven { url 'https://jitpack.io' }
    mavenLocal()
}

shadowJar {
    dependencies {
        include(dependency('org.apache.commons:.*'))
        include(dependency('com.google.code.gson:.*'))
        include(dependency('com.github.mizosoft.methanol:.*'))
        include(dependency('com.knuddels:.*'))
        include(dependency('org.apache.httpcomponents:.*'))
    }
}